import java.text.SimpleDateFormat

plugins {
    id "java"
    id "io.freefair.lombok" version "5.3.0"
}

group 'com.solace.tools.sempcfg'
version '1.0.3'
def longVersion = version + ", Build time: "+ new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())

repositories {
    // Use JCenter for resolving dependencies.
    jcenter()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    archiveFileName = rootProject.name+".jar"
    manifest {
        attributes "Main-Class": "com.solace.tools.sempcfg.App",
                'Implementation-Title': rootProject.name,
                'Implementation-Version': longVersion
    }

    from {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    // This dependency is used by the application.
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation 'info.picocli:picocli:4.6.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'com.jayway.jsonpath:json-path:2.5.0'
    implementation 'org.freemarker:freemarker:2.3.31'
}

tasks.named('test') {
    // Use junit platform for unit tests.
    useJUnitPlatform()
}
